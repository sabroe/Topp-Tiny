plugins {
    id 'java-library-distribution'
    id 'maven-publish'
    id 'signing'
}

//group 'io.github.sabroe.topp'
//version '0.0.4-test'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation 'org.junit.platform:junit-platform-suite-api'
}

test {
    useJUnitPlatform()
    testLogging {
//        events "passed", "skipped", "failed"
        events "skipped", "failed"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = 'topp-tiny-concurrent-utility'
            version = project.version
            from components.java
            pom {
                name = 'Topp-Tiny-Concurrent'
                description = 'Topp Tiny Concurrent is an open-source Java library which implements concurrency related utilities.'
                url = 'https://github.com/sabroe/Topp-Tiny'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/sabroe/Topp-Tiny/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sabroe'
                        name = 'Morten Sabroe Mortensen'
                        email = 'morten.sabroe.mortensen@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com:sabroe/Topp-Tiny.git'
                    developerConnection = 'scm:git:ssh://github.com:sabroe/Topp-Tiny.git'
                    url = 'https://github.com/sabroe/Topp-Tiny'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties['ossrhUsername']
                password = project.properties['ossrhPassword']
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
